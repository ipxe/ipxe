#!ipxe
echo === NIC DISCOVERY ==================================
# Must start at 0; uint does not start at 0.
set idx:int8 0
:check isset ${net${idx}/mac} || goto checked
  echo net${idx} MAC ${net${idx}/mac} PCI.DeviceID ${pci/${net${idx}/busloc}.2.2} PCI.VendorID ${pci/${net${idx}/busloc}.0.2}
  inc idx && goto check
:checked

echo === LINK ===========================================
set vidx:int8 0
:linkcheck isset ${net${vidx}/mac} || goto opened
  ifopen net${vidx} && ifstat net${vidx} || echo Failed to open net${vidx}
  inc vidx && goto linkcheck
:opened

echo === DHCP ===========================================
# Shut down all interfaces and open them as needed. Sternly prevents iPXE from
# making bad decisions selecting which interface traffic egresses.
ifclose
set vidx:int8 0
:dhcpcheck isset ${net${vidx}/mac} || goto configured
  ifopen net${vidx} || echo Failed to open net${vidx}
  ifconf -c dhcp --timeout 20000 net${vidx} && goto configured || ifclose net${vidx}
  inc vidx && goto dhcpcheck
:configured
echo Dumping interface status:
isset ${net${vidx}/ip} && show net${vidx}/ip || echo No interfaces were configured
ifstat

echo === Loading Menu ... ===============================
# This should be coded to always point to our LiveCD alias and nothing else.
# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x86_64 || set arch x86
cpuid --ext 29 && set archb 64 || set archb 32
cpuid --ext 29 && set archl x86_64 || set archl i386
colour --basic 0 4
cpair --background 4 0
set menu-timeout 2000
# Assume the DNS server is the same server PXE booting us.
set server-ip ${dhcp-server}
set script-url http://${dhcp-server}
# If the hostname is set in the DHCPOFFER use that for a personalized script.
isset ${hostname} && set script-url ${script-url}/${hostname}/script.ipxe || set script-url ${script-url}/boot/script.ipxe
:start
menu Metal Pre-Boot :: ${manufacturer} ${product} (${archb}bit)
item --gap -- ---------------- Boot Choices ----------------
item --key d default    (D)efault next (${script-url})
item --key c change         (C)hange next to ...
item --key r reboot         (R)eboot
item --key e bios           (E)xit to BIOS Menu
item
item --gap -- ---------------- Advanced configuration ----------------
item --key i config   Configure / View (i)PXE settings
item --key s shell    Enter iPXE (s)hell
item
choose --default default --timeout ${menu-timeout} target || goto cancel ||
set menu-timeout 0
goto ${target}

:bios
echo Exiting PXE ROM, dropping into BIOS ...
exit 0

:change
read script-url
goto default

:config
config
goto start

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
goto start

:cancel
echo Menu cancelled, dropping to iPXE shell ...
goto shell

:default
echo Calling home (server-ip=${server-ip})...
isset server || goto failed_ping
set pingidx:int8 1
:call_home
  # Only use -c 1, otherwise a successful ping will continue needlessly.
  # This loop will ensure an unsuccessful ping retries without holding up a successful ping.
  echo Dumping interface status:
  isset ${net${vidx}/ip} && show net${vidx}/ip || echo No interfaces were configured
  ifstat
  echo show routes:
  route
  echo attempt: ${pingidx} of 30
  ping -c 1 ${server-ip} && goto called_home ||
  iseq ${pingidx} 30 && goto failed_ping ||
  iseq ${pingidx} 30 || inc pingidx && goto call_home
:called_home
echo Successfully called home; deployment server is reachable
goto boot

:boot
echo querying ${script-url}
chain --autofree --replace ${script-url} || goto failed

:failed_ping
echo Failed to call home! ${server-ip} was not reachable.
goto failed

:failed
echo Exiting iPXE; depending on vendor this will drop to the next PXE interface or to BIOS.
echo If BIOS appears next, select either of the DISK entries.
exit 1
