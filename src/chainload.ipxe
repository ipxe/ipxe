#!ipxe
set vtag:int8 2
echo ====NIC DISCOVERY===================================
# Must start at 0; uint does not start at 0.
set idx:int8 0
:check isset ${net${idx}/mac} || goto checked
  echo net${idx} MAC ${net${idx}/mac} PCI.DeviceID ${pci/${net${idx}/busloc}.2.2} PCI.VendorID ${pci/${net${idx}/busloc}.0.2}
  inc idx && goto check
:checked

echo ====DHCP ===========================================
set vidx:int8 0
:vcheck isset ${net${vidx}/mac} || goto done
  vcreate --tag ${vtag} net${vidx}
  dhcp net${vidx}-${vtag} && goto done ||
  inc vidx && goto vcheck
:done

echo ====Loading ...=====================================
# This should be coded to always point to our LiveCD alias and nothing else.
# Figure out if client is 64-bit capable
cpuid --ext 29 && set arch x86_64 || set arch x86
cpuid --ext 29 && set archb 64 || set archb 32
cpuid --ext 29 && set archl x86_64 || set archl i386
colour --basic 0 4
cpair --background 4 0
set menu-timeout 2000
# Assume the DNS server is the same server PXE booting us.
set script-url http://${dhcp-server}
# If the hostname is set in the DHCPOFFER use that for a personalized script.
isset ${hostname} && set script-url ${script-url}/${hostname}/script.ipxe || set script-url ${script-url}/boot/script.ipxe
:start
menu Metal Pre-Boot :: ${manufacturer} ${product} (${archb}bit)
item --gap -- ---------------- Boot Choices ----------------
item --key d default    (D)efault next (${script-url})
item --key c change         (C)hange next to ...
item --key r reboot         (R)eboot
item --key e exit           (E)xit to BIOS Menu
item
item --gap -- ---------------- Advanced configuration ----------------
item --key i config   Configure / View (i)PXE settings
item --key s shell    Enter iPXE (s)hell
item
choose --default default --timeout ${menu-timeout} target || goto cancel ||
set menu-timeout 0
goto ${target}

:failed
echo Booting failed, dropping to shell
goto shell

:change
read script-url
goto default

:default
echo querying ${script-url}
chain ${script-url} || goto start

:config
config
goto start

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
goto start

:exit
echo Exiting iPXE, loading BIOS Menu
exit

:cancel
echo Menu cancelled, dropping to iPXE shell..
goto shell
